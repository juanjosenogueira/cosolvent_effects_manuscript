#!/usr/bin/gnuplot

set terminal epslatex size 15.50cm,14.0cm dashed color
set output 'equilibration.tex'

set rmargin at screen 0.98
set multiplot layout 3,1
set xrange [0:200]
set yrange [0:6]
set ytics 0,1,6
set xtics 0,25,200

set ylabel 'RMSD [\AA]'
# set xlabel 'Simulated time [ns]'

set key at 200,1.0

set arrow 1 from 40,0 to 40, 3 lw 8 lc rgb "#000000" dt 3 nohead
set arrow 2 from 40,1 to 55, 1 lw 5 lc rgb "#000000" 
set label '\textcolor{black}{equilibrated}' at 57,1

p 'water.rms' using (($1)/1000):($2) w l lw 5 lc rgb "#009BFA" t '\textcolor{juliablue}{water solvent}'

unset arrow
unset label

set arrow 1 from 25,0 to 25, 4.5 lw 8 lc rgb "#000000" dt 3 nohead
set arrow 2 from 25,1 to 40, 1 lw 5 lc rgb "#000000"
set label '\textcolor{black}{equilibrated}' at 42,1

p 'benzene.rms' using (($1)/1000):($2) w l lw 5 lc rgb "#E26E46" t '\textcolor{juliared}{benzene cosolvent}'

unset arrow
unset label
set xlabel 'Simulated time [ns]'
set size 1,0.35

set arrow 1 from 10,0 to 10, 3.5 lw 8 lc rgb "#000000" dt 3 nohead
set arrow 2 from 10,1 to 25, 1 lw 5 lc rgb "#000000"
set label '\textcolor{black}{equilibrated}' at 27,1

p 'phenol.rms' using (($1)/1000):($2) w l lw 5 lc rgb "#3EA44D" t '\textcolor{juliagreen}{phenol cosolvent}'

unset multiplot

set output
!sed -i 's|{equilibration}|{../equilibration}|' equilibration.tex
!epstopdf equilibration.eps --outfile='equilibration.pdf'
!rm -f equilibration.eps
